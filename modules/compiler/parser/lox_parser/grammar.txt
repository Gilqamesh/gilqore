Formal grammar:
expression     -> literal
                | unary
                | binary
                | grouping ;

literal        -> NUMBER | STRING | "true" | "false" | "nil" ;
grouping       -> "(" expression ")" ;
unary          -> ( "-" | "!" ) expression ;
binary         -> expression operator expression ;
operator       -> "==" | "!=" | "<" | "<=" | ">" | ">="
                | "+"  | "-"  | "*" | "/" ;

Precedence and Associativity:

program         -> statement* EOF ;
statement       -> expr_statement
                 | print_statement ;
expr_statement  -> expression ";" ;
print_statement -> "print" expression ";" ;
expression      -> comma ;
comma           -> ternary ( "," expression)* ;
ternary         -> equality ( "?" expression ":" expression )* ;
equality        -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            -> factor ( ( "-" | "+" ) factor )* ;
factor          -> unary ( ( "/" | "*" ) unary )* ;
unary           -> ( "!" | "-" ) unary
                 | primary ;
primary         -> NUMBER | STRING | "true" | "false" | "nil"
                 | "(" expression ")" 
                 | error ;
error           -> "," | "?" | ":" | "!=" | "==" | ">" | ">=" |
                 | "<" | "<=" | "+"
