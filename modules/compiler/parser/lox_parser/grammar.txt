program         -> declaration* EOF;
declaration     -> fun_decl
                 | var_decl
                 | statement ;
fun_decl        -> "fun" function ;
var_decl        -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement       -> expr_stmt
                 | for_stmt
                 | if_stmt
                 | print_stmt
                 | while_stmt
                 | break_stmt
                 | continue_stmt
                 | block
                 | return_stmt ;
function        -> IDENTIFIER lambda_expr ;
lambda_expr     -> "(" parameters? ")" block ;
expr_stmt       -> expression ";" ;
for_stmt        -> "for" "(" ( var_decl | expr_stmt | ";" )
                   expression? ";"
                   expression? ")" statement ;
if_stmt         -> "if" "(" expression ")" statement
                    ( "else" statement )? ;
print_stmt      -> "print" expression ";" ;
while_stmt      -> "while" "(" expression ")" statement ;
break_stmt      -> "break" ";" ;
continue_stmt   -> "continue" ";" ;
block           -> "{" declaration* "}" ;
return_stmt     -> "return" expression? ";" ;
parameters      -> IDENTIFIER ( "," IDENTIFIER)* ;
expression      -> comma ;
comma           -> assignment ( "," expression)* ;
assignment      -> ternary
                 | IDENTIFIER "=" assignment ;
ternary         -> logical_or ( "?" expression ":" expression )* ;
logical_or      -> logical_and ( "or" logical_and )* ;
logical_and     -> equality ( "and" equality )* ;
equality        -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            -> factor ( ( "-" | "+" ) factor )* ;
factor          -> unary ( ( "/" | "*" | "%" ) unary )* ;
unary           -> ( "!" | "-" ) unary
                 | "fun" lambda_expr
                 | call ;
call            -> primary ( "(" arguments? ")" )* ;
arguments       -> expression ( "," expression )* ;
primary         -> NUMBER | STRING | "true" | "false" | "nil"
                 | "(" expression ")"
                 | IDENTIFIER
                 | error ;
error           -> "," | "?" | ":" | "!=" | "==" | ">" | ">=" |
                 | "<" | "<=" | "+" | "break" | "continue" ;
